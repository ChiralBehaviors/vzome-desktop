
plugins {
    id 'java'
    id 'maven'
    id 'eclipse'
    id 'application'
}

// these environment variables are available during a Jenkins build
ext.buildNumber = System.getenv( "BUILD_NUMBER" ) ?: 'SNAPSHOT'
ext.gitCommit = System.getenv( "GIT_COMMIT" ) ?: 'HEAD'
ext.appName = 'vZome'
ext.edition = appName

group = 'com.vzome'
version = "5.0"

description = 'vzome-desktop'

//sourceCompatibility = 1.5
//targetCompatibility = 1.5

task recordBuildProperties {
    def propsFile = file( "$buildDir/buildPropsResource/build.properties" )
    ext.outputDir = propsFile .getParentFile()
    doFirst {
        writeProjectProperties( [ 'version', 'edition', 'buildNumber', 'gitCommit' ], propsFile )
    }
}
processResources { 
    dependsOn recordBuildProperties
    from recordBuildProperties.outputDir
} 


// these are for both 'application' and 'macappbundle' Gradle plugins
ext.jvmArgs = [
//    "java.ext.dirs": ".",
    "apple.laf.useScreenMenuBar": "true",
    "apple.awt.antialiasing": "true",
    "apple.awt.application.name": appName,
    "com.apple.macos.use-file-dialog-packages": "true",
    "com.apple.macos.useScreenMenuBar": "true",
    "com.apple.smallTabs": "true",
    "java.util.logging.config.file": "logging.properties" ]
ext.vzomeArgs = [  // a list, not a map
    '-entitlement.model.edit', 'true', '-entitlement.lesson.edit', 'true', '-entitlement.all.tools', 'true',
    '-licensed.user', System.getProperty('user.name') ]

// for the 'application' Gradle plugin
mainClassName = 'org.vorthmann.zome.ui.ApplicationUI'
run.args( vzomeArgs )
applicationDefaultJvmArgs = [ '-Xmx3048M' ]
jvmArgs.each { entry ->
    applicationDefaultJvmArgs << "-D$entry.key=$entry.value"
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        
        // this one is for javax.media.jai
        maven { url 'http://repository.springsource.com/maven/bundles/external' }

        ivy { // not really an Ivy repo, but this pattern lets us automate the bare JAR downloads for java3d/*
            url "http://jogamp.org/deployment"
            layout "pattern", {
                artifact "[organization]/[revision]/[artifact].[ext]"
            }
        }
    }
}

dependencies {
    compile group: 'com.vzome.core', name: 'vzome-core', version:'0.2'
    compile group: 'java3d', name: 'j3dcore', version:'1.6.0-pre11'
    compile group: 'java3d', name: 'j3dutils', version:'1.6.0-pre11'
    compile group: 'java3d', name: 'vecmath', version:'1.6.0-pre11'
    compile group: 'org.jogamp.gluegen', name: 'gluegen-rt-main', version:'2.2.4'
    compile group: 'org.jogamp.jogl', name: 'jogl-all-main', version:'2.2.4'
    compile group: 'javax.media.jai', name: 'com.springsource.javax.media.jai.core', version:'1.1.3'
    testCompile group: 'junit', name: 'junit', version:'3.8.1'
}

sourceSets {
    main {
        java {
            exclude 'org/vorthmann/zome/render/jogl/**'  // initial framework for a pure JOGL renderer is not ready for use
        }
        resources {
            // The convention-based default built-in srcDir is 'src/main/resources' 
            // Append any additional SrcDir entries here...
            
            // listed alphabetically
            include '**/*.gif'
            include '**/*.jpg'
            include '**/*.pdf'
            include '**/*.png'
            include '**/*.properties'
            include '**/*.py'
            include '**/*.vef'
            include '**/*.vZome'
            include '**/*.zomic'
        }
    }
}

// The mainClass property is needed to run a Gradle project from the Netbeans IDE (and maybe others too).
if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}

// Specify common settings for all subprojects
subprojects {
    buildDir    = rootProject.buildDir
    group       = rootProject.group
    version     = rootProject.version
    description = rootProject.name + '-' + project.name
}

def writeProjectProperties( keys, propsFile )
{
    def outputDir = propsFile .getParentFile()
    outputDir.exists() || outputDir.mkdirs()
    def buildProps = new Properties()
    project.properties.findAll( { it.key in keys } ) .each {
        buildProps .put( it.key, it.value.toString() )
    }
    def writer = new FileWriter( propsFile )
    try {
        buildProps .store( writer, null )
        writer.flush()
    } finally {
        writer.close()
    }
}
